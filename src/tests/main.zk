let name: string = @input => |"Enter your name: "|;

@println => |"Welcome to the game " + name + "!\nAt any time, type 'stats' to view your stats or 'inv' to view your inventory."|

let player: obj = {
  name: name,
  gold: 100,
  health: 100,
  inventory: {
    Sword: {
      name: "Basic Iron Sword",
      damage: 10,
      durability: 100
    },
  },
};

let goblin: obj = {
  name: "Goblin",
  health: 10,
  gold: 20,
  damage: 5
};

func show_stats |player: obj| {
  @println => |"\nPlayer Stats:"|
  @println => |"Name: " + player.name|
  @println => |"Health: " + player.health|
  @println => |"Gold: " + player.gold|
  @println => |""|
}

func show_inventory |player: obj| {
  @println => |"\nInventory:"|
  for |item, details| in player.inventory {
    @println => |item + ": " + details.name + " (Damage: " + details.damage + ", Durability: " + details.durability + ")\n"|
  }
  @println => |""|
}

func combat |player: obj, goblin: obj| {
    let combat_ended: bool = false;
    while combat_ended != true {
        let choice: string = @input => |"\nCombat Options:\n1. Attack\n2. Defend\n3. Use Item\nEnter your choice (or type 'stats'/'inv'): "|;

        if choice == "stats" {
            show_stats => |player|
        } else if choice == "inv" {
            show_inventory => |player|
        } else if choice == "1" {
            goblin.health = goblin.health - player.inventory.Sword.damage
            @println => |"You deal " + player.inventory.Sword.damage + " damage to " + goblin.name + "!"|
            
            if goblin.health <= 0 {
                @println => |"\nYou defeated the " + goblin.name + "!"|
                @println => |"You gained " + goblin.gold + " gold!"|
                player.gold = player.gold + goblin.gold
            } else {
                player.health = player.health - goblin.damage
                @println => |goblin.name + " deals " + goblin.damage + " damage to you!"|
                
                if player.health <= 0 {
                    @println => |"\nYou have been defeated..."|
                }
            }
            combat_ended = true
        } else if choice == "2" {
            reduced_damage = goblin.damage / 2
            player.health = player.health - reduced_damage
            @println => |"You defend against the attack!"|
            @println => |goblin.name + " deals " + reduced_damage + " damage to you!"|
            
            if player.health <= 0 {
                @println => |"\nYou have been defeated..."|
                combat_ended = true
            }
        } else if choice == "3" {
            @println => |"No items available to use!"|
        } else {
            @println => |"Invalid choice, please try again."|
        }
    }
}

func left_path |player: obj, goblin: obj| {
    let combat_choice: bool = false;
    while combat_choice != true {
        let choice: string = @input => |"\nYou encounter a " + goblin.name + " with " + goblin.health + " health!\n1. Fight\n2. Run away\nEnter your choice (or type 'stats'/'inv'): "|;

        if choice == "stats" {
            show_stats => |player|
        } else if choice == "inv" {
            show_inventory => |player|
        } else if choice == "1" {
            @println => |"You engage in combat with the " + goblin.name + "!"|
            combat => |player, goblin|
            combat_choice = true
        } else if choice == "2" {
            @println => |"You run away safely!"|
            combat_choice = true
        } else {
            @println => |"Invalid choice, please try again."|
        }
    }
}

func shop |player: obj| {
    let shopping: bool = false;
    let shop_inventory: obj = {
        BasicSword: {
            name: "Basic Iron Sword",
            damage: 10,
            durability: 100,
            price: 50
        },
        HealthPotion: {
            name: "Health Potion",
            healing: 25,
            price: 30
        },
        SteelSword: {
            name: "Steel Sword",
            damage: 25,
            durability: 150,
            price: 120
        }
    };

    while shopping != true {
        @println => |"\nWelcome to the shop! You have " + player.gold + " gold."|
        let choice: string = @input => |"Available Items:\n1. Basic Iron Sword (50 gold) - 10 damage\n2. Health Potion (30 gold) - Heals 25 HP\n3. Steel Sword (120 gold) - 25 damage\n4. Leave Shop\nEnter your choice (or type 'stats'/'inv'): "|;

        if choice == "stats" {
            show_stats => |player|
        } else if choice == "inv" {
            show_inventory => |player|
        } else if choice == "1" && player.gold >= 50 {
            player.gold = player.gold - 50
            player.inventory.BasicSword = shop_inventory.BasicSword
            @println => |"You bought a Basic Iron Sword!"|
        } else if choice == "2" && player.gold >= 30 {
            player.gold = player.gold - 30
            if player.inventory.HealthPotion {
                player.inventory.HealthPotion.amount = player.inventory.HealthPotion.amount + 1
            } else {
                player.inventory.HealthPotion = shop_inventory.HealthPotion
                player.inventory.HealthPotion.amount = 1
            }
            @println => |"You bought a Health Potion!"|
        } else if choice == "3" && player.gold >= 120 {
            player.gold = player.gold - 120
            player.inventory.SteelSword = shop_inventory.SteelSword
            @println => |"You bought a Steel Sword!"|
        } else if choice == "4" {
            @println => |"Thank you for visiting!"|
            shopping = true
        } else if choice == "1" || choice == "2" || choice == "3" {
            @println => |"Not enough gold!"|
        } else {
            @println => |"Invalid choice, please try again."|
        }
    }
}

func right_path |player: obj| {
    let village_choice: bool = false;
    while village_choice != true {
        let choice: string = @input => |"\nYou arrive at the village. What would you like to do?\n1. Visit the shop\n2. Talk to villagers\n3. Leave village\nEnter your choice (or type 'stats'/'inv'): "|;

        if choice == "stats" {
            show_stats => |player|
        } else if choice == "inv" {
            show_inventory => |player|
        } else if choice == "1" {
            @println => |"You enter the village shop..."|
            shop => |player|
            @println => |"\nYou return to the village square."|
        } else if choice == "2" {
            @println => |"The villagers tell you about a dragon in the mountains..."|
            @println => |"'Be careful traveler, only the strongest warriors can defeat it!'"|
        } else if choice == "3" {
            @println => |"You leave the village behind..."|
            village_choice = true
        } else {
            @println => |"Invalid choice, please try again."|
        }
    }
}

let choice_made: bool = false;
while choice_made != true {
    let choice_one: string = @input => |"You stumble across a fork in a path, do you\n1. Take the left path\nOR\n2. Take the right path\nEnter your choice (or type 'stats'/'inv'): "|;

    if choice_one == "stats" {
        show_stats => |player|
    } else if choice_one == "inv" {
        show_inventory => |player|
    } else if choice_one == "1" {
        @println => |"You take the left pathway in the fork, you keep walking until you find a hoard of goblins!"|
        left_path => |player, goblin|
        choice_made = true
    } else if choice_one == "2" {
        @println => |"You take the right pathway in the fork, you walk to a nearby village."|
        right_path => |player|
        choice_made = true
    } else {
        @println => |"Invalid choice, please try again."|
    }
}